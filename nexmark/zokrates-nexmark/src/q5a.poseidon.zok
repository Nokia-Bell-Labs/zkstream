// Copyright 2024 Nokia
// Licensed under the BSD 3-Clause Clear License.
// SPDX-License-Identifier: BSD-3-Clause-Clear

from "./shared" import
    PublicKey,
    Signature,
    MessageMetadata,
    Bids,
    Bid,
    HashPoseidon as Hash,
    SaltPoseidon as Salt,
    verifySignaturePoseidonEddsa as verifySignature,
    hashOutputPoseidon as hashOutput;
from "./gadgets" import
    count;
from "utils/casts" import cast;

// Number of messages.
const u32 N_MESSAGES = 100;

def main(
    // Public key of sensor
    PublicKey publicKey,
    // Metadata of messages
    MessageMetadata[N_MESSAGES] msgs,
    // Values of messages
    private Bids<N_MESSAGES> bids,
    // Signatures of messages
    private Signature[N_MESSAGES] signatures,
    // Salt to be used for output
    private Salt outputSalt
// count (private), auction (public), hashed count (public)
) -> (u64, u64, Hash) {
    // 1. Verify all signatures
    for u32 i in 0..N_MESSAGES {
        bool isZero = i >= bids.n;
        assert(verifySignature(publicKey, msgs[i], bids.values[i], signatures[i], isZero));
    }
    // 2. Check that the auction ID is the same for all messages
    u64 auction = bids.values[0].auction;
    for u32 i in 1..N_MESSAGES {
        bool isZero = i >= bids.n;
        assert(isZero || bids.values[i].auction == auction);
    }
    // 3. Calculate count
    u32 cnt_u32 = count(bids);
    u64 cnt_u64 = cast(cnt_u32);
    // 4. Hash outputs
    Hash cnt_hashed = hashOutput(cnt_u64, outputSalt);
    return (cnt_u64, auction, cnt_hashed);
}
